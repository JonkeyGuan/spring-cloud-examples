buildscript {
  ext {
    asciidocBuildDir = "$buildDir/asciidoc"
  }
}

plugins {
  id "org.asciidoctor.convert" version "1.5.3"
  id "org.ajoberstar.git-publish" version "0.2.2"
}

group "daggerok"
version "0.4.1"

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$asciidocBuildDir")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/spring-cloud-examples"
}

asciidoctor.doLast {
  file("$asciidocBuildDir/html5/404.html").text = file("$asciidocBuildDir/html5/index.html").text
  file("$asciidocBuildDir/html5/.nojekyll").text = "\n"
}

asciidoctor.shouldRunAfter clean

// read more: https://github.com/ajoberstar/gradle-git-publish
gitPublish {
  repoUri = "git@github.com:daggerok/spring-cloud-examples.git"
  branch = "gh-pages"
  contents {
    from "$asciidocBuildDir/html5"
    from "$projectDir/src/static"
    into "."
  }
  commitMessage = "Publish documentation for ${project.group}:${project.name}:$project.version"
}


gitPublishPush.dependsOn asciidoctor
gitPublishPush.shouldRunAfter clean, asciidoctor, build

task docs(dependsOn: gitPublishPush)
