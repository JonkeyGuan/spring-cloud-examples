buildscript {
  ext {
    springBootVersion = "1.5.7.RELEASE"
    springCloudVersion = "Dalston.SR3"
    springCloudTaskVersion = "1.2.1.RELEASE"
    propertiesPluginEnvironmentNameProperty = "env"
    environmentFileDir = "env"
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
  }
}

plugins {
  id "idea"
  id "eclipse"
  id "net.saliman.properties" version "1.4.6"
  //id "org.springframework.boot" version "1.5.7.RELEASE" apply false
}

subprojects { subproject ->

  if (isDocker(subproject)) return

  apply plugin: "java"
  //apply plugin: "groovy"

  group = "daggerok"
  version = "0.0.1"
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

  repositories {
    mavenCentral()
  }

  apply plugin: "org.springframework.boot"

  dependencies {
    compileOnly("org.projectlombok:lombok")
    compile("org.springframework.cloud:spring-cloud-starter-stream-rabbit")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("io.vavr:vavr:0.9.1")
    testCompile("org.springframework.boot:spring-boot-starter-test")
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
      mavenBom "org.springframework.cloud:spring-cloud-task-dependencies:$springCloudTaskVersion"
    }
  }

  springBoot.executable = true
}

def isDocker(Project p) {
  def absPath = p.projectDir.absolutePath
  def projectPath = absPath.replaceFirst(rootDir.absolutePath, "")
  projectPath.contains("docker")
}

wrapper {
  gradleVersion = "4.2"
  distributionType = "ALL"
}
